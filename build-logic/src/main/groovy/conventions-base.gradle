plugins {
    id 'com.diffplug.spotless'
}

group = PROJECT_GROUP
version = PROJECT_VERSION

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = PROJECT_GROUP
            artifactId = PROJECT_NAME
            version = PROJECT_VERSION
        }
    }
    repositories {
        mavenLocal()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
    compileJava {
        dependsOn spotlessApply
    }
}

sourcesJar {
    from sourceSets.main.allJava
    archiveBaseName = PROJECT_NAME
    archiveVersion = PROJECT_VERSION
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

javadoc {
    enabled = true
    source = sourceSets.main.allJava
    classpath += configurations.compileClasspath
    options {
        encoding = 'UTF-8'
        title = PROJECT_NAME
        windowTitle = PROJECT_NAME + ":" + PROJECT_VERSION + "-Javadoc"
    }
    failOnError = false
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        formatAnnotations()
    }
    groovyGradle {
        target '**/*.gradle'
        trimTrailingWhitespace()
        endWithNewline()
    }
    css {
        target 'src/**/*.css'
        prettier()
        trimTrailingWhitespace()
        endWithNewline()
    }
}
